+++
title = "Fix aeson ToJSONKey/FromJSONKey derivation"
date = 2024-03-17
draft = false
path = "2024-03/aeson-key"

[taxonomies]
categories = ["dev"]
tags = ["haskell"]
+++

In Haskell, we have [`aeson`](https://hackage.haskell.org/package/aeson) which is
a library providing reading and writing JSON.

It's mainly done through two `type class`es:
[`ToJSON`](https://hackage.haskell.org/package/aeson-2.2.1.0/docs/Data-Aeson.html#t:ToJSON)/[`FromJSON`](https://hackage.haskell.org/package/aeson-2.2.1.0/docs/Data-Aeson.html#t:FromJSON).

We can easily create a simple type and use it:

```haskell
data RegularWeekend = RSaturday | RSunday
  deriving stock (Eq, Ord, Show, Generic)
  deriving anyclass (FromJSON, FromJSONKey, ToJSON, ToJSONKey)

encode RSunday -- "RSunday"
```

Now if we try to use it on the key part:

```haskell
encode (Map.singleton RSunday ("Happy day" :: String)) -- "[[\"RSunday\",\"Happy day\"]]"
```

What happened here!?

Let's see how the derivation is done in
[`ToJSONKey`](https://hackage.haskell.org/package/aeson-2.2.1.0/docs/Data-Aeson.html#t:ToJSONKey)/[`FromJSONKey`](https://hackage.haskell.org/package/aeson-2.2.1.0/docs/Data-Aeson.html#t:FromJSONKey)
(instances responsible for JSON keys generation/rendering):

```haskell
class ToJSONKey a where
    -- | Strategy for rendering the key for a map-like container.
    toJSONKey :: ToJSONKeyFunction a
    default toJSONKey :: ToJSON a => ToJSONKeyFunction a
    toJSONKey = ToJSONKeyValue toJSON toEncoding
```

The reason is clear, everything is based on `ToJSON`'s `toJSON' which produces
a `Value`, which can be any JSON types, so we cannot treat it as a string key.

To mitigate it, we have to create a derivation with produce only JSON strings,
but to abstract this a bit, let's start with a simple pair of`type class`es:

```haskell
class ToSumText a where
  toSumText :: a -> T.Text

class FromSumText a where
  fromSumText :: T.Text -> Maybe a
```

Also, we provide a `newtype` which allows proper `DerivingVia` behavior:

```haskell
newtype PureSum a = PureSum {unPureSum :: a}
  deriving stock (Eq, Ord, Show)
```

The [`Generics`](https://hackage.haskell.org/package/base-4.19.1.0/docs/GHC-Generics.html),
machinery is beyond the scope of this log, but it prevents misuses:

```haskell
data X0
  deriving stock (Generic)

deriving via (PureSum X0) instance ToSumText X0
```

being and empty datatype it yields a compile-time error:

```
    • Only pure sum types are supported (constructor(s) without values)
    • In the third argument of ‘ghc-prim-0.10.0:GHC.Prim.coerce’, namely
        ‘(toSumText @(PureSum X0))’
      In the expression:
        ghc-prim-0.10.0:GHC.Prim.coerce
          @(PureSum X0 -> Data.Text.Internal.Text)
          @(X0 -> Data.Text.Internal.Text) (toSumText @(PureSum X0))
      In an equation for ‘toSumText’:
          toSumText
            = ghc-prim-0.10.0:GHC.Prim.coerce
                @(PureSum X0 -> Data.Text.Internal.Text)
                @(X0 -> Data.Text.Internal.Text) (toSumText @(PureSum X0))
      When typechecking the code for ‘toSumText’
        in a derived instance for ‘ToSumText X0’:
        To see the code I am typechecking, use -ddump-deriv
   |
   | deriving via (PureSum X0) instance ToSumText X0
```

Given the previous `type classs`es, we can implement `aeson` instances properly:

```haskell
instance (FromSumText a, Generic a, GConstructorName (Rep a)) => FromJSON (PureSumWith transformation a) where
  parseJSON = withText (unConstructionName $ (to @a) <$> gConstructorName) pureSumWithParser

instance (FromSumText a, Generic a, GConstructorName (Rep a)) => FromJSONKey (PureSumWith transformation a) where
  fromJSONKey = FromJSONKeyTextParser pureSumWithParser

pureSumWithParser :: (FromSumText a) => T.Text -> Parser (PureSumWith transformation a)
pureSumWithParser x =
  maybe (fail $ "unknown value: " <> show x) (pure . PureSumWith) $
    fromSumText x

newtype ConstructorName x = ConstructorName {unConstructionName :: String}

instance Functor ConstructorName where
  fmap _ (ConstructorName x) = ConstructorName x

class GConstructorName f where
  gConstructorName :: ConstructorName (f a)

instance (KnownSymbol typeName) => GConstructorName (M1 D ('MetaData typeName c i b) a) where -- base type
  gConstructorName = ConstructorName $ symbolVal (Proxy @typeName)

instance (ToSumText a) => ToJSON (PureSumWith transformation a) where
  toJSON = toJSON . toSumText . unPureSumWith
  toEncoding = toEncoding . toSumText . unPureSumWith

instance (ToSumText a) => ToJSONKey (PureSumWith transformation a) where
  toJSONKey = toJSONKeyText (toSumText . unPureSumWith)
```

There are two important points in the code above:

* `aeson` provides `toJSONKeyText`/`FromJSONKeyTextParser` which narrows down parsing to `Text` `Value`, this is why we can easily plug `FromSumText`/`ToSumText`
* All the `GConstructorName` part is leveraging GHC's [`Generics`](https://hackage.haskell.org/package/base-4.19.1.0/docs/GHC-Generics.html), which is a way to represent values structure at type-level, feel free to have a look at the haddock page, the snippet only focuses on extracting the type name present in one of the structure type's metadata (note: the deriving mechanics skipped earlier is way more complex)

Let see it in action:

```haskell
data BetterWeekend = BSaturday | BSunday
  deriving stock (Eq, Ord, Show, Generic)
  deriving (ToSumText, FromSumText, FromJSON, FromJSONKey, ToJSON, ToJSONKey) via (PureSum BetterWeekend)
```

And how it runs:

```haskell
encode (Map.singleton BSunday ("Happy day" :: String)) -- "{\"BSunday\":\"Happy day\"}"
eitherDecode "{\"BSunday\":\"Happy day\"}" -- Right (Map.singleton BSunday ("Happy day" :: String))
```

